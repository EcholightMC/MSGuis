plugins {
    id "java"
    id "maven-publish"
}

group = "com.github.echolightmc"
version = "1.4-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly "net.minestom:minestom-snapshots:1d0f512256"
    testImplementation("net.minestom:minestom-snapshots:1d0f512256")
    implementation "net.kyori:adventure-text-minimessage:4.16.0"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = rootProject.name
            version = this.version
            from components.java

            pom {
                name = artifactId
                description = "Easy GUI library made for Minestom"
            }
        }
    }
    repositories {
        maven {
            credentials {
                // Avoid hard failure when these are not provided (e.g. local builds / CI test runs).
                // Provide via Gradle properties: -PrepoHapilyUsername=... -PrepoHapilyPassword=...
                // or environment variables: REPO_HAPILY_USERNAME / REPO_HAPILY_PASSWORD.
                username = (findProperty("repoHapilyUsername") ?: System.getenv("REPO_HAPILY_USERNAME") ?: "").toString()
                password = (findProperty("repoHapilyPassword") ?: System.getenv("REPO_HAPILY_PASSWORD") ?: "").toString()
            }

            url = "https://maven.hapily.me/releases"
        }
    }
}